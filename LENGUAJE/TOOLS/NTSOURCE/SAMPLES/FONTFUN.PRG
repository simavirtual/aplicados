*********** Sample programs for Nantucket Tools II / 5.01 ************
*
* Program     : FONTFUN.PRG
* Copyright   : (c) 1991, Nantucket  Corp.  All rights reserved.
* Date        : 12/11/90
* Author      : Lothar Bongartz
* Description : Pseudo graphics with font functions
* Compiling   : CLIPPER FONTFUN
* Linking     : RTLINK FI FONTFUN, NT2US50 LIB NT250
* Remarks     : In the Nantucket Tools no graphic functions are
*               available. The graphics shown in this program
*               are generated by fonts with a minimum pixel height.
*               This program has been created for a better
*               understanding of the font system and should not
*               be taken too seriously!
*
*******************************************************************

* Graphic WITHOUT mode shift, only with fonts:
*
 cOldFont = GETFONT()
 cFont    = REPLICATE(CHR(0), 512)  // 256 characters, 2 pixelrows ea.

 FOR i = 8 TO 1 STEP -1             // from left to the right
  cTemp = CHR(SETBIT(0, i))         // set one pixel
  POSREPL(@cFont, cTemp +cTemp, 1 +2 *(8 -i))  // 1, 3, 5, 7, 9, 11, 13, 15
 NEXT i

 cTemp = CHR(SETBIT(0, 4, 5))       // the two centered pixel
 POSREPL(@cFont, cTemp +cTemp, 17)  // for CHR(8)

 SETFONT(cFont, .T.)

*The following function does not work with each screen adapter!
*Therefore, it is marked with '*'.
*OUTWORD("3C4", 257)                // 8 instead of 9 horizontal pixel
                                    // to avoid a horizontal space
 CLEAR SCREEN                       // blank does not have any
                                    // 6 lines of characters

  FOR i = 0 TO 158                  // EGA: MAXROW() = 174
   @ i, i /2 SAY CHR(4 *(i %2))     // VGA:            199
   @ i, i /4 SAY CHR(2 *(i %4))
   @ i, i /8 SAY CHR(    i %8 )

   @ i, 79 -    i /2  SAY CHR(   4 *   (i %2))
   @ i, 79 -INT(i /4) SAY CHR(7 -2 *INT(i %4))
   @ i, 79 -INT(i /8) SAY CHR(7 -   INT(i %8))
  NEXT i

 INKEY(4)
 CLEAR SCREEN

 nMaxScan = CHARPIX() *(MAXROW() +1) -1   // vertical screen pixel
 nScan4   = nMaxScan /4                   // 2 pixel per character
                                          // 2 max. vertical value

 FOR i = 0 TO PI() *3 STEP 0.125          // 1 /8 - coarse sine
  @ nScan4 *(1+ SIN(i)), i *8 SAY CHR(8)  // SIN() from -1 to +1
 NEXT i

 INKEY(4)
 CLEAR SCREEN

 FOR j = 0.109375 TO 0 STEP -0.015625     // 1 /64 // fine sine
  FOR i = 0 TO PI() *3 STEP 0.125         // 1 /8
   @ nScan4 *(1+ SIN(i +j)), (i +j) *8 SAY CHR(j *64)
  NEXT i
 NEXT j

 INKEY(4)
 CLEAR SCREEN

SETFONT(cOldFont, .T.)

